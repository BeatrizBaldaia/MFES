class Auditorium is subclass of Installation
instance variables
	public foyers: set of Foyer := {};
operations
-- Constructor (must have telephones and movingWalls always false)
	public Auditorium: Utils_vdm`String * real * nat * real * real * real * bool * bool * bool * bool * bool * bool ==> Auditorium
		Auditorium(i, pr, c, h, w, l, airC, natL, ceilL, blackC, compN, soundP) == (
			--Measures
			id := i;
			price := pr;
			capacity := c;
			heigth := h;
			atomic(width := w; lenght := l; area := w * l);
			--Conditions
			airCondition := airC;
			naturalLigth := natL;
			ceilingLighting := ceilL;
			blackOutCurtains := blackC;
			computerNetwork := compN;
			soundproofWalls := soundP;
		)
		pre c > 0 and h > 0 and w > 0 and l > 0 and pr > 0
		post not (telephones or movingWalls);

--Impossible to create a installation with out parameters
		public Auditorium: () ==> Auditorium
		Auditorium() == return self
		pre false;

--Change installation's conditions	
	public setConditions: bool * bool * bool * bool * bool * bool * bool * bool ==> ()
		setConditions(airC, natL, ceilL, blackC, t, compN, soundW, m) == (
			airCondition := airC;
			naturalLigth := natL;
			ceilingLighting := ceilL;
			blackOutCurtains := blackC;
			computerNetwork := compN;
			soundproofWalls := soundW
		)
		pre not (t or m)
		post not (telephones or movingWalls);
		
--Shows all installation information
	public showDetails: () ==> map Utils_vdm`String to Utils_vdm`String
	showDetails() == (
		dcl res: map Utils_vdm`String to Utils_vdm`String := {|->};
		res := res ++ {
				"ID" |-> id, "Price" |-> Utils_vdm`toStringVDM(price), "Capacity" |-> Utils_vdm`toStringVDM(capacity),
				"Area" |-> Utils_vdm`toStringVDM(area), "Heigth" |-> Utils_vdm`toStringVDM(heigth), 
				"Width" |-> Utils_vdm`toStringVDM(width), "Lenght" |-> Utils_vdm`toStringVDM(lenght), 
				"Air Condition" |-> Utils_vdm`toStringVDM(airCondition), 
				"Natural Ligth" |-> Utils_vdm`toStringVDM(naturalLigth),
				"Ceiling Lighting" |-> Utils_vdm`toStringVDM(ceilingLighting),
				"Black Out Curtains" |-> Utils_vdm`toStringVDM(blackOutCurtains),
				"Computer Network" |-> Utils_vdm`toStringVDM(computerNetwork),
				"Soundproof Walls" |-> Utils_vdm`toStringVDM(soundproofWalls)
		};
		return res
	);
				
-- Add foyer to foyers set
	public addFoyer : Foyer ==> ()
		addFoyer(f) == foyers := foyers union {f}
		pre f not in set foyers
		post	card foyers = card foyers~ + 1 and f in set foyers; 
		
-- Remove foyer from foyers set
	public removeFoyer : Foyer ==> ()
		removeFoyer(f) == foyers := foyers \ {f}
		pre f in set foyers
		post card foyers = card foyers~ - 1 and f not in set foyers;

	public addRoom : Room ==> ()
		addRoom(-) == return
		pre false;
	public removeRoom : Room ==> ()
		removeRoom(-) == return
		pre false;

--Verify if has installation
	public hasInstallation: Installation ==> bool
		hasInstallation(inst) == (
			if(isofclass(Foyer, inst)) then return (let f = narrow_(inst, Foyer) in f in set foyers)
			else return false
		)
		pre inst <> self;
end Auditorium
