class Center
types
	public DateType = <begin> | <ending>;
	public UserPosition = <attendee> | <staff> | <host>;
values
 private adminName: Utils`String = "admin";
 private adminPass: Utils`String = "admin1234";
 
instance variables
	public name: Utils`String;
	public installations: map Utils`String to Installation;
	inv card dom installations > 0 and 
		forall id in set dom installations &  installations(id).id = id;
	public services: map Service`ServiceType to Service := {|->};
	inv forall type in set dom services & services(type).type = type;
	public events: map Utils`String to Event := {|->};
	public users: map Utils`String to User := {|->};
	
operations
	public Center:  Utils`String * Installation ==> Center
		Center(n, inst) == (
			let instID = inst.getID() in
			installations := {instID |-> inst};
			name := n;
		)
		post card dom installations = 1 and name = n;

--INSTALLATION OPERATIONS
-- Add one installation to installations set		
	public addInstallations: Utils`String * Installation ==> ()
		addInstallations(uName, inst) == let instID = inst.getID() in 
		installations := installations ++ {instID |-> inst}
		pre uName in set dom users and uName = "admin" and
			inst not in set rng installations and isofbaseclass(Installation, inst)
		post card dom installations = card dom installations~ + 1 and inst in set rng installations;
	
-- Add installation, foyer or room, to other installation, auditorium or pavilion\
	public addInstallationToInstallation: Utils`String * Utils`String * Installation ==> ()
	addInstallationToInstallation(uName, instID, inst1) == (
		installations := installations ++ {inst1.id |-> inst1};
		def inst2 = installations(instID) in (
			if(isofclass(Foyer, inst1)) then def foyer = narrow_(inst1, Foyer) in inst2.addFoyer(foyer)
			elseif(isofclass(Room, inst1)) then def room = narrow_(inst1, Room) in inst2.addRoom(room)
			--TODO: else error 
		)
	)
	pre uName in set dom users and uName = "admin" and
		instID in set dom installations and
		(isofclass(Foyer, inst1) or isofclass(Room, inst1)) and
		not exists inst in set rng installations & (
			(isofclass(Auditorium, inst) or isofclass(Pavilion, inst)) and
			hasInstallation(inst1, inst)
		) and (
			(isofclass(Foyer, inst1) and (isofclass(Auditorium, installations(instID)) or isofclass(Pavilion, installations(instID)))) or
			(isofclass(Room, inst1) and isofclass(Pavilion, installations(instID)))
		)
	post inst1 in set rng installations
		and hasInstallation(inst1,installations(instID));
	
-- Remove installation, foyer or room, from other installation, auditorium or pavilion\
	public removeInstallationFromInstallation: Utils`String * Utils`String * Installation ==> ()
	removeInstallationFromInstallation(uName, instID, inst1) == (
		def inst2 = installations(instID) in (
			if(isofclass(Foyer, inst1)) then def foyer = narrow_(inst1, Foyer) in inst2.removeFoyer(foyer)
			elseif(isofclass(Room, inst1)) then def room = narrow_(inst1, Room) in inst2.removeRoom(room)
			--TODO: else error 
		)
	)
	pre uName in set dom users and uName = "admin" and
		instID in set dom installations and
		inst1 in set rng installations and
		(isofclass(Foyer, inst1) or isofclass(Room, inst1)) and
		let inst = installations(instID) in hasInstallation(inst1, inst) and (
			(isofclass(Foyer, inst1) and (isofclass(Auditorium, installations(instID)) or isofclass(Pavilion, installations(instID)))) or
			(isofclass(Room, inst1) and isofclass(Pavilion, installations(instID)))
		)
	post inst1 in set rng installations and
	not hasInstallation(inst1, installations(instID));
	
--Remove installation from installations map
	public removeInstallation: Utils`String * Utils`String ==> ()
	removeInstallation(uName, instID) == (
		let inst in set rng installations \ {installations(instID)} in
			if(hasInstallation(installations(instID), inst)) then removeInstallationFromInstallation(uName, inst.id, installations(instID));
		installations := {instID} <-: installations
	)
	pre uName in set dom users and uName = "admin" and
		instID in set dom installations
	post instID not in set dom installations and
	not exists inst in set rng installations & hasInstallation(installations~(instID), inst);

--Gets all the installations available in the given period
	public getAvailableInstallations: Utils`Date * Utils`Date ==> map Utils`String to Installation
	getAvailableInstallations(b, e) == (
		dcl tmpUsedInsts: set of Installation := {};
		dcl usedInsts: set of Installation := {};
		dcl tmpFreeInsts: map Utils`String to Installation := {|->};
		let ev in set rng Utils`fMap[Utils`String, bool, Event](lambda x: Event & hasDatesConflict(x, b, e))(Utils`setTOseq[Utils`String](dom events), events) in (
			tmpUsedInsts :=  tmpUsedInsts union {ev.installation}
		);
		tmpFreeInsts := installations :-> usedInsts;
		usedInsts := tmpUsedInsts;
		let inst1 in set rng tmpFreeInsts in (
			let inst2 in set tmpUsedInsts in (
				if(inst1.hasInstallation(inst2)) then usedInsts :=  usedInsts union {inst1}
			)
		);
		return tmpFreeInsts :-> usedInsts;
	)
	pre Utils`before(b, e) or b = e
	post rng RESULT subset rng installations and
	forall ev in set rng Utils`fMap[Utils`String, bool, Event](lambda x: Event & hasDatesConflict(x, b, e))(Utils`setTOseq[Utils`String](dom events), events) &
	ev.installation not in set rng RESULT;
	
--Checks if this two installations are the same or associated
-- (if inst2 is part of inst1)
	public associatedInstallations: Installation * Installation ==> bool
	associatedInstallations(inst1, inst2) == (
		if(inst1 = inst2) then return true
		else inst1.hasInstallation(inst2)
	)
	pre {inst1, inst2} subset rng installations;

--SERVICE OPERATIONS
-- Add one service to services set
	public addService: Utils`String * Service ==> ()
		addService(uName, serv) == let type = serv.type in
		services := services ++ {type |-> serv}
		pre uName in set dom users and uName = "admin" and
			serv not in set rng services
		post card dom services = card dom services~ + 1 and serv in set rng services;

--Remove service from services map
	public removeService: Utils`String * Service`ServiceType ==> ()
		removeService(uName, service) == (
			let e in set {x | x in set rng events & service in set elems x.services} in
				e.removeService(service);
			services := {service} <-: services
		)
		pre uName in set dom users and uName = "admin" and
			service in set dom services
		post service not in set dom services and
			forall e in set rng events & service not in set elems e.services;
		
-- Changes a service price
	public changeServicePrice: Utils`String * Service`ServiceType * real ==> ()
	changeServicePrice(uName, serviceType, price) == def service = services(serviceType) in
	service.setPrice(price)
	pre uName in set dom users and uName = "admin" and
		price > 0 and serviceType in set dom services
	post services(serviceType).price = price;

--EVENT OPERATIONS
--Create an event	and add it to the events map
	public createEvent:  Utils`String * nat * real * Utils`Date * Utils`Date * bool * Event`EventType * Installation * Utils`String ==> ()
	createEvent(n, tTickets, tPrice, b, e, p, t, inst, h) == (
		dcl event: Event := new Event(n, tTickets, tPrice, b, e, p, t, inst, h);
		events := events ++ {n |-> event}
	)
	pre h in set dom users and n not in set dom events and 
		(Utils`before(b, e) or b = e) and
		forall ev in set rng Utils`fMap[Utils`String, bool, Event](lambda x: Event & hasDatesConflict(x, b, e))(Utils`setTOseq[Utils`String](dom events), events) &
			ev.installation <> inst
	post n in set dom events;
	
--Return how much an event spent in services
	public moneySpentWithServices: Utils`String * Utils`String ==> real
	moneySpentWithServices(uName, evName) == (
		dcl ev: Event := events(evName);
		dcl res: real := 0;
		let service in seq ev.services in res := res + services(service).price;
		return res;
	)
	pre uName in set dom users and (uName = events(evName).host or uName = "admin") and
	 evName in set dom events;
	
--TODO: Return how much an event spent with installation rent
	public moneySpentWithInstallation: Utils`String * Utils`String ==> real
	moneySpentWithInstallation(uName, evName) == (
		dcl days: int := Utils`getDatesDifference(events(evName).begin, events(evName).ending);
		return days * events(evName).ticketPrice
	)
	pre uName in set dom users and (uName = events(evName).host or uName = "admin") and
	 evName in set dom events;
--In case of a private event, host can invite users (so they will be
-- the only ones capable of seeing and buying a ticket to the event)
	public inviteToEvent: Utils`String * Utils`String * Utils`String ==> ()
	inviteToEvent(evName, hName, uName) == events(evName).inviteUser(hName, uName)
	pre evName in set dom events and uName in set dom users and
		hName in set dom users and hName <> uName and
		events(evName).privacy = true
	post uName in set events(evName).guests;

--Shows all the available events
	public showAvailableEvents: Utils`String ==> [set of Event]
	showAvailableEvents(uName) == (
		dcl res: set of Event := {};
		let e in set rng events in (
			if(card e.attendees = e.totalTickets) then skip;
			if(e.privacy and uName in set e.guests) then res := res union {e};
			if(not e.privacy) then res := res union {e}
		);
		return res
	)
	pre uName in set dom users
	post RESULT subset rng events;

--Shows all the available events in a period
	public showAvailableEventsBetweenDates: Utils`String * Utils`Date * Utils`Date ==> [set of Event]
	showAvailableEventsBetweenDates(uName, b, e) == (
		dcl res: set of Event := {};
		let ev in set rng events in 
			if(hasDatesConflict(ev, b, e)) then (
				if(card ev.attendees = ev.totalTickets) then skip;
				if(ev.privacy and uName in set ev.guests) then res := res union {ev};
				if(not ev.privacy) then res := res union {ev}
			);
		return res	
	)
	pre uName in set dom users and (Utils`before(b, e) or b = e)
	post RESULT subset rng events and
		forall event in set RESULT & hasDatesConflict(event, b, e);
		
--OPERATIONS TO CHANGE EVENTS ATTRIBUTES	
--Change event name
	public changeEventName: Utils`String * Utils`String * Utils`String ==> ()
	changeEventName(evName, hName, value) == (
		dcl ev: Event := events(evName);
		ev.setName(value);
		events := events ++ {value |-> ev}
	)
	pre evName in set dom events and value not in set dom events and
		hName in set dom users and events(evName).host = hName
	post evName not in set dom events and value in set dom events;

--Change event number of tickets
	public changeEventTotalTickets: Utils`String * Utils`String * nat ==> ()
	changeEventTotalTickets(evName, hName, value) == (
		dcl ev: Event := events(evName);
		ev.setTotalTickets(value)
		--events := events ++ {evName |-> ev} Se ev for uma referencia, nao e preciso atualizar
	)
	pre evName in set dom events and value > 0 and
		hName in set dom users and events(evName).host = hName
	post events(evName).totalTickets = value;

--Change event ticket price
	public changeEventTicketPrice: Utils`String * Utils`String * real ==> ()
	changeEventTicketPrice(evName, hName, value) == (
		dcl ev: Event := events(evName);
		ev.setTicketPrice(value)
		--events := events ++ {evName |-> ev}
	)
	pre evName in set dom events and value >= 0 and
		hName in set dom users and events(evName).host = hName
	post events(evName).ticketPrice = value;

--Change event date (begin or ending)
	public changeEventDate: Utils`String * Utils`String * DateType * Utils`Date ==> ()
	changeEventDate(evName, hName, dateType, value) == (
		dcl ev: Event := events(evName);
		cases dateType:
			<begin> -> ev.setBeginDate(value),
			<ending> -> ev.setEndingDate(value)
			--others -> error
			end
	)
	pre evName in set dom events and (dateType = <begin> or dateType = <ending>) and
		hName in set dom users and events(evName).host = hName
	post if(dateType = <begin>) then events(evName).begin = value
		else events(evName).ending = value;

--Change event privacy condition
	public changeEventPrivacy: Utils`String * Utils`String * bool ==> ()
	changeEventPrivacy(evName, hName, value) == (
		dcl ev: Event := events(evName);
		ev.setPrivacy(value)
	)
	pre evName in set dom events and
		hName in set dom users and events(evName).host = hName
	post events(evName).privacy = value;

--Change event type
	public changeEventType: Utils`String * Utils`String * Event`EventType ==> ()
	changeEventType(evName, hName, value) == (
		dcl ev: Event := events(evName);
		ev.setType(value)
	)
	pre evName in set dom events and
		hName in set dom users and events(evName).host = hName
	post events(evName).type = value;

--Change where event will occur
	public changeEventInstallation: Utils`String * Utils`String * Utils`String ==> ()
	changeEventInstallation(evName, hName, instName) == (
		dcl ev: Event := events(evName);
		ev.changeInstallation(installations(instName))
	)
	pre evName in set dom events and
		let b = events(evName).begin, e = events(evName).ending in (
			forall ev in set rng Utils`fMap[Utils`String, bool, Event](lambda x: Event & hasDatesConflict(x, b, e))(Utils`setTOseq[Utils`String](dom events), events) &
			instName <> ev.installation.id
		) and
		hName in set dom users and events(evName).host = hName
	post events(evName).installation.id = instName;

--Add service to event
	public addServiceToEvent: Utils`String * Utils`String * Service`ServiceType ==> ()
	addServiceToEvent(evName, hName, servType) == events(evName).addService(servType)
	pre evName in set dom events and servType in set dom services and
		hName in set dom users and events(evName).host = hName
	post servType in set elems events(evName).services;

--Remove service from event
	public removeServiceFromEvent: Utils`String * Utils`String * Service`ServiceType ==> ()
	removeServiceFromEvent(evName, hName, servType) == events(evName).removeService(servType)
	pre evName in set dom events and servType in set dom services and
		servType in set elems events(evName).services and
		hName in set dom users and events(evName).host = hName
	post servType not in set elems events(evName).services;
	
--USER FUNCTIONS
--Add user to our center
	public addUser: User ==> ()
	addUser(user) == users := users ++ {user.name |-> user}
	pre user.name not in set dom users
	post user in set rng users;
	
--Add user to event
	public addUserToEvent: Utils`String * Utils`String * UserPosition ==> ()
	addUserToEvent(evName, uName, uPos) == (
		dcl ev: Event := events(evName);
		dcl user: User := users(uName);
		cases uPos:
			<attendee> -> (ev.addAttendee(uName); user.addEvent(evName)),
			<staff> -> (ev.addStaff(uName); user.addEvent(evName)),
			<host> -> (ev.setHost(uName); user.addEvent(evName))
		end
	)
	pre evName in set dom events and uName in set dom users and
		uPos in set {<attendee>, <staff>, <host>}
	post evName in set users(uName).events;
	--Remove user from event
	public removeUserFromEvent: Utils`String * Utils`String * UserPosition ==> ()
	removeUserFromEvent(evName, uName, uPos) == (
		dcl ev: Event := events(evName);
		dcl user: User := users(uName);
		cases uPos:
			<attendee> -> (ev.removeAttendee(uName); user.removeEvent(evName)),
			<staff> -> (ev.removeStaff(uName); user.removeEvent(evName))
		end
	)
	pre evName in set dom events and uName in set dom users and
		uPos in set {<attendee>, <staff>}
	post evName not in set users(uName).events;

functions
-- Verify if an installation has a specific installation (room or foyer)
	public hasInstallation: Installation * Installation -> bool
	hasInstallation(inst1, inst2) == (
		if (isofclass(Auditorium, inst2)) then (
			let inst = narrow_(inst2, Auditorium) in
			if(isofclass(Foyer, inst1)) then let foyer = narrow_(inst1, Foyer) in foyer in set inst.foyers
			else false
		) elseif(isofclass(Pavilion, inst2)) then (
			let inst = narrow_(inst2, Pavilion) in
			if(isofclass(Foyer, inst1)) then let foyer = narrow_(inst1, Foyer) in foyer in set inst.foyers
			else let room = narrow_(inst1, Room) in room in set inst.rooms
		) else (
			false
		)
	)
	pre true;--(isofclass(Foyer, inst1) or isofclass(Room, inst1)) and
	--(isofclass(Auditorium, inst2) or isofclass(Pavilion, inst2));
	
--Checks if this event has conflicts with the period given in the arguments
	public hasDatesConflict: Event * Utils`Date * Utils`Date -> bool
	hasDatesConflict(ev, b2, e2) == (
		def b1 = ev.begin; e1 = ev.ending in
		if(Utils`before(b2, b1)) then Utils`before(b1, e2)
		else Utils`before(b2, e1)
	)
	pre (Utils`before(ev.begin, ev.ending) or ev.begin = ev.ending) 
		and (Utils`before(b2, e2) or b2 = e2);
	
end Center