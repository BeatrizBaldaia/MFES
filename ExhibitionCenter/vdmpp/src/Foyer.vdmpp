class Foyer is subclass of Installation
operations
-- Constructor (must have blackOutCurtains, telephones, soundproofWalls and movingWalls always false)
	public Foyer: Utils_vdm`String * real * nat * real * real * real * bool * bool * bool * bool ==> Foyer
		Foyer(i, pr, c, h, w, l, airC, natL, ceilL, compN) == (
			--Measures
			id := i;
			price := pr;
			capacity := c;
			heigth := h;
			atomic(width := w; lenght := l; area := w * l);
			--Conditions
			airCondition := airC;
			naturalLigth := natL;
			ceilingLighting := ceilL;
			computerNetwork := compN;
		)
		pre c > 0 and h > 0 and w > 0 and l > 0
		post not (blackOutCurtains or telephones or soundproofWalls or movingWalls);
--Impossible to create a installation with out parameters
	public Foyer: () ==> Foyer
		Foyer() == return self
		pre false;

--Change installation's conditions	
	public setConditions: bool * bool * bool * bool * bool * bool * bool * bool ==> ()
		setConditions(airC, natL, ceilL, b, t, compN, s, m) == (
			airCondition := airC;
			naturalLigth := natL;
			ceilingLighting := ceilL;
			computerNetwork := compN
		)
		pre not (b or t or s or m)
		post not (blackOutCurtains or telephones or soundproofWalls or movingWalls);
		
--Shows all installation information
	public showDetails: () ==> map Utils_vdm`String to Utils_vdm`String
	showDetails() == (
		dcl res: map Utils_vdm`String to Utils_vdm`String := {|->};
		res := res ++ {
				"ID" |-> id, "Price" |-> Utils_vdm`toStringVDM(price), "Capacity" |-> Utils_vdm`toStringVDM(capacity),
				"Area" |-> Utils_vdm`toStringVDM(area), "Heigth" |-> Utils_vdm`toStringVDM(heigth), 
				"Width" |-> Utils_vdm`toStringVDM(width), "Lenght" |-> Utils_vdm`toStringVDM(lenght), 
				"Air Condition" |-> Utils_vdm`toStringVDM(airCondition), 
				"Natural Ligth" |-> Utils_vdm`toStringVDM(naturalLigth),
				"Ceiling Lighting" |-> Utils_vdm`toStringVDM(ceilingLighting),
				"Computer Network" |-> Utils_vdm`toStringVDM(computerNetwork)
		};
		return res
	);
	
	public addFoyer : Foyer ==> ()
		addFoyer(-) == return
		pre false;
	public removeFoyer : Foyer ==> ()
		removeFoyer(-) == return
		pre false;
	public addRoom : Room ==> ()
		addRoom(-) == return
		pre false;
	public removeRoom : Room ==> ()
		removeRoom(-) == return
		pre false;
--Verify if has installation
	public hasInstallation: Installation ==> bool
		hasInstallation(inst) == return false
		pre inst <> self;
end Foyer